{
    parserClass="cz.cuni.mff.kyjovsm.robocop.parser.RobotFrameworkParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="RobotFramework"
    psiImplClassSuffix="Impl"
    psiPackage="cz.cuni.mff.kyjovsm.robocop.psi"
    psiImplPackage="cz.cuni.mff.kyjovsm.robocop.psi.impl"

    elementTypeHolderClass="cz.cuni.mff.kyjovsm.robocop.psi.RobotFrameworkTypes"
    elementTypeClass="cz.cuni.mff.kyjovsm.robocop.psi.RobotFrameworkElementType"
    tokenTypeClass="cz.cuni.mff.kyjovsm.robocop.psi.RobotFrameworkTokenType"

    psiImplUtilClass="cz.cuni.mff.kyjovsm.robocop.psi.impl.RobotFrameworkPsiImplUtil"
}


EMPTY_LINE ::= END_OF_LINE* NEW_LINE_TOKEN { pin=2 }
private END_OF_LINE ::= COLUMN_SEP_TOKEN | WHITESPACE_TOKEN | COMMENT_TOKEN
private not_a_newline_token ::= ! NEWLINE_TOKEN
COLON ::= COLON_TOKEN


/*Definition of the top-level Robot Framework File Sections
i.e *** SETTINGS ***, *** VARIABLES ***, etc. */
robotFrameworkFile ::= (TABLE | EMPTY_LINE)* END_OF_LINE*

TABLE ::= (SETTINGS_TABLE | VARIABLES_TABLE | TEST_CASES_TABLE | KEYWORDS_TABLE)


/*Configuration of grammar for Settings Section*/
SETTINGS_TABLE ::= SETTINGS_TABLE_HEADER (SETTINGS_PARAMETER EMPTY_LINE | EMPTY_LINE)* (SETTINGS_PARAMETER END_OF_LINE* <<eof>>)? { pin=2 }
SETTINGS_TABLE_HEADER ::= SETTINGS_TABLE_HEADER_TOKEN EMPTY_LINE
SETTINGS_PARAMETER ::= WHITESPACE_TOKEN SETTING END_OF_LINE* |
                   RECURRENCE_PARAMETER_SETTINGS END_OF_LINE* {recoverWhile="not_a_newline_token"}

private RECURRENCE_PARAMETER_SETTINGS ::= COLUMN_SEP_TOKEN? WHITESPACE_TOKEN? RECURRENCE

SETTING ::= LIBRARY_SETTING |
            VARIABLES_SETTING |
            RESOURCE_SETTING |
            DOCUMENTATION_SETTING |
            METADATA_SETTING |
            SUITE_SETUP_SETTING |
            SUITE_TEARDOWN_SETTING |
            FORCE_TAGS_SETTING |
            DEFAULT_TAGS_SETTING |
            TEST_SETUP_SETTING |
            TEST_TEARDOWN_SETTING |
            TEST_TEMPLATE_SETTING |
            TEST_TIMEOUT_SETTING

LIBRARY_SETTING ::= LIBRARY_SETTING_TOKEN COLUMN_SEP_TOKEN LIBRARY_REFERENCE SETTING_LIST_OR_RECURRENCE* { pin=3 }
LIBRARY_REFERENCE ::= LIBRARY_TOKEN {methods=[getReference getReferences]}
SETTING_LIST_OR_RECURRENCE ::= SETTING_LIST | RECURRENCE_FOR_SETTING_TABLE
SETTING_LIST ::= (COLUMN_SEP_TOKEN KEYWORD_ARG)+
RECURRENCE_FOR_SETTING_TABLE ::= EMPTY_LINE WHITESPACE_TOKEN? COLUMN_SEP_TOKEN? RECURRENCE
RECURRENCE ::= RECURRENCE_TOKEN

VARIABLES_SETTING ::= VARIABLES_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE* COLUMN_SEP_TOKEN REFERENCED_FILE { pin=1 }
RESOURCE_SETTING ::= RESOURCE_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE* COLUMN_SEP_TOKEN REFERENCED_FILE { pin=1 }
REFERENCED_FILE ::= RF_FILE_TOKEN { methods=[getReference getReferences getName setName getNameIdentifier handleElementRename getPresentation toString]}

DOCUMENTATION_SETTING ::= DOCUMENTATION_SETTING_TOKEN DOCUMENTATION_OR_RECURRENCE* { pin=1 }
private DOCUMENTATION_OR_RECURRENCE ::= (COLUMN_SEP_TOKEN DOCUMENTATION_TOKEN) | RECURRENCE_FOR_SETTING_TABLE { pin=1 }

FORCE_TAGS_SETTING ::= FORCE_TAGS_SETTING_TOKEN TAG_LIST_OR_RECURRENCE_NO_COL* { pin = 1 }


DEFAULT_TAGS_SETTING ::= DEFAULT_TAGS_SETTING_TOKEN TAG_LIST_OR_RECURRENCE_NO_COL* {pin=1}

private TAG_LIST_OR_RECURRENCE_NO_COL ::= TAG_LIST | RECURRENCE_FOR_SETTING_TABLE
private TAG_LIST ::= (COLUMN_SEP_TOKEN TAG)+
TAG ::= TAG_TOKEN

METADATA_SETTING ::= METADATA_SETTING_TOKEN SETTING_LIST_OR_RECURRENCE* { pin=1 }

TEST_TIMEOUT_SETTING ::= TEST_TIMEOUT_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE+ COLUMN_SEP_TOKEN TIMEOUT_UNITS_TOKEN |
                         TEST_TIMEOUT_SETTING_TOKEN  COLUMN_SEP_TOKEN TIMEOUT_UNITS_TOKEN { pin=1 }

TEST_TEMPLATE_SETTING ::= TEST_TEMPLATE_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE* COLUMN_SEP_TOKEN KEYWORD

SUITE_SETUP_SETTING ::= SUITE_SETUP_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE+ COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS |
                        SUITE_SETUP_SETTING_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS { pin=1 }

SUITE_TEARDOWN_SETTING ::= SUITE_TEARDOWN_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE+ COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS |
                        SUITE_TEARDOWN_SETTING_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS { pin=1 }

TEST_TEARDOWN_SETTING ::= TEST_TEARDOWN_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE+ COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS |
                        TEST_TEARDOWN_SETTING_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS { pin=1 }

TEST_SETUP_SETTING ::= TEST_SETUP_SETTING_TOKEN RECURRENCE_FOR_SETTING_TABLE+ COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS |
                       TEST_SETUP_SETTING_TOKEN COLUMN_SEP_TOKEN KEYWORD_INVOCATION_SETTINGS { pin=1 }


